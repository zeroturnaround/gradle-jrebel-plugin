/**
 * To deploy to local maven repository, run:
 * ./gradlew publishToMavenLocal
 *
 * To release to Sonatype's central nexus, run:
 *   ./gradlew clean publish -Prelease_to_central
 *
 * To release to gradle plugin portal run:
 *   ./gradlew clean publishPlugins
 */
plugins {
  id "com.gradle.plugin-publish" version "0.9.9"
  id "org.sonarqube" version "2.6"
  id "java-gradle-plugin"
}

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'project-report'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

// Artifact settings

project.group = 'org.zeroturnaround'
project.version = '1.1.11-SNAPSHOT'

project.ext.name = 'JRebel Gradle plugin'
project.ext.description = 'JRebel Gradle plugin'
project.ext.vcs = 'https://github.com/zeroturnaround/gradle-jrebel-plugin'
project.ext.website = 'https://jrebel.com'

// Compiler settings (Gradle 2.0 requires Java 6+)
targetCompatibility = "1.6"
sourceCompatibility = "1.6"

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

configurations {
  deployerJars
  published.extendsFrom archives, signatures
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
  def outputDir = file("$buildDir/$name")

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

// add version of the plugin into MANIFEST.MF to add it into generated rebel.xml files
jar {
  manifest {
    attributes("Implementation-Version":project.version)
  }
}

javadoc {
  options.addBooleanOption('Xdoclint:none', true)
}

tasks.withType(GenerateModuleMetadata) {
  enabled = false
}

// === Dependency management

dependencies {
  compileOnly gradleApi()

  implementation 'commons-lang:commons-lang:2.6'
  implementation 'commons-io:commons-io:2.5'

  testImplementation gradleTestKit()
  testImplementation 'junit:junit:4.13.1'
  testImplementation 'xmlunit:xmlunit:1.6'
  testImplementation 'org.hamcrest:hamcrest-library:1.3'
  testImplementation 'commons-io:commons-io:2.6'

  deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:2.12'

  testRuntimeOnly files(createClasspathManifest)
}

repositories {
  mavenCentral()
}

// =========================== TESTING ===========================

// Run tests before installing the artifact
jar.dependsOn(test)

// =========================== PUBLISHING ===========================

java {
  withSourcesJar()
  withJavadocJar()
}

pluginBundle {
  website = project.ext.website
  vcsUrl = project.ext.vcs
  description = project.ext.description
  tags = ['JRebel', 'rebel.xml', 'ZeroTurnaround']
}

gradlePlugin {
  plugins {
    jrebel {
      id = 'org.zeroturnaround.gradle.jrebel'
      implementationClass = 'org.zeroturnaround.jrebel.gradle.RebelPlugin'
    }
  }
}

// Required data for maven central
def pomFile = {
  name = project.ext.name
  description = project.ext.description
  url = project.ext.website
  licenses {
    license {
      name = 'The Apache Software License, Version 2.0'
      url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution = 'repo'
    }
  }
  developers {
    developer {
      id = 'jrebel_support'
      name = 'JRebel Support'
      email = 'support@jrebel.com'
    }
  }
  inceptionYear = '2011'
  scm {
    url = 'https://github.com/zeroturnaround/gradle-jrebel-plugin'
    connection = 'scm:git:git@github.com:zeroturnaround/gradle-jrebel-plugin.git'
    developerConnection = 'scm:git:git@github.com:zeroturnaround/gradle-jrebel-plugin.git'
  }
}

publishing {
  publications {
    pluginMaven(MavenPublication) { // publication name is defined by java-gradle-plugin
      pom pomFile
    }
  }
  repositories {
    maven {
      // Use sonatype central
      if (project.hasProperty("release_to_central")) {
        if (project.version.endsWith("-SNAPSHOT")) {
          name = 'sonatypeNexusSnapshots'
          url = 'https://oss.sonatype.org/content/repositories/snapshots'
        } else {
          name = 'sonatypeNexusStaging'
          url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        }
      }
      // Use zeroturnaround public repo
      else {
        if (project.version.endsWith("-SNAPSHOT")) {
          name = 'zeroturnaroundNexusSnapshots'
          url = 'https://repos.zeroturnaround.com/nexus/content/repositories/zt-public-snapshots/'
        } else {
          name = 'zeroturnaroundNexusReleases'
          url = 'https://repos.zeroturnaround.com/nexus/content/repositories/zt-public-releases/'
        }
      }
      // name+"Username" and name+"Password" are looked up as credential properties
      credentials(PasswordCredentials)
    }
  }
}

afterEvaluate {
  publishing {
    publications {
      // Created by java-gradle-plugin very late, can't be configured above
      jrebelPluginMarkerMaven(MavenPublication) {
        pom pomFile
      }
    }
  }
  tasks.withType(PublishToMavenRepository) {
    // Releases can be published to plugin portal, don't need the marker nonsense (gradle could have designed plugin id as group:id so no redirection or conventions needed)
    onlyIf {
      project.version.endsWith("-SNAPSHOT") || publication != publishing.publications.jrebelPluginMarkerMaven
    }
  }
}

signing {
  required {
    gradle.taskGraph.hasTask(publish) || gradle.taskGraph.hasTask(publishPlugins)
  }
  sign publishing.publications.pluginMaven
}

// java-gradle-plugin creates name+"PluginMarkerMaven" in afterEvaluate
afterEvaluate {
  signing {
    sign publishing.publications.jrebelPluginMarkerMaven
  }
}
